plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.zqf.imdevproject"
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file('D:\\opensource_workspace\\IMDevProject\\app\\imxkeystore.jks')
            storePassword 'imxkeystore'
            keyAlias 'imx'
            keyPassword 'imxkeystore'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "default"
    productFlavors {
        CHANNELS.each { channel ->
            "$channel" {
                buildConfigField "String", "CHANNEL_VALUE", "\"$name\""
                if (channel == "huawei") {
                    resValue "string", "app_name", "华为市场名字"
                } else if (channel == "vivo") {
                    resValue "string", "app_name", "VIVO市场名字"
                } else {
                    resValue "string", "app_name", "缘起"
                }
            }
        }
    }
    android.applicationVariants.all { variant ->
        def apkName = variant.productFlavors[0].name
        def buildTypeName = variant.buildType.name
        def versionName = defaultConfig.versionName
        variant.outputs.all {
            def fileName = "${apkName}_${releaseTime()}_${buildTypeName}_${versionName}.apk"
            outputFileName = fileName
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation project(path: ':imx')
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.multidex:multidex:2.0.1'
}
